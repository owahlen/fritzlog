apply plugin: 'java'
apply plugin: 'groovy'
apply plugin: 'application'
apply plugin: 'idea'
apply plugin: 'maven'

def jdkVersion = '1.6'

sourceCompatibility = jdkVersion
targetCompatibility = jdkVersion

mainClassName = 'com.infinit.fritzlog.Main'

group = 'com.infinit.fritzlog'
version = '1.0'

// The idea closure configure the idea plugin.
// The command "gradle idea" will setup a reasonable idea structure
idea {
	project {
		jdkName = jdkVersion
		languageLevel = jdkVersion
	}
	module {
		inheritOutputDirs = false
		outputDir = file('build/classes/main')
		testOutputDir = file('build/classes/test')

		iml {
			withXml { xml ->
				Node moduleNode = xml.asNode()
				moduleNode.component[0].children().last().plus {
					orderEntry(type: 'module-library', scope:"RUNTIME") {
						library {
							CLASSES {
								root(url: 'file://\$MODULE_DIR\$/src/main/resources')
							}
							JAVADOCK()
							SOURCES()
						}
					}
					orderEntry(type: 'module-library', scope:"TEST") {
						library {
							CLASSES {
								root(url: 'file://\$MODULE_DIR\$/build/resources/test')
							}
							JAVADOCK()
							SOURCES()
						}
					}
				}
			}
		}
	}
}

repositories {
	mavenCentral()
	mavenRepo name: 'Codehaus', url: 'http://repository.codehaus.org'
}

dependencies {

	compile('commons-cli:commons-cli:1.2')
	compile('joda-time:joda-time:2.2')
	compile('log4j:log4j:1.2.17') {
		transitive = false
	}
	compile 'net.sourceforge.htmlunit:htmlunit:2.12'
	compile('org.codehaus.groovy:groovy-all:2.1.3')

	testCompile 'org.spockframework:spock-core:0.7-groovy-2.0'
	testCompile 'cglib:cglib-nodep:2.2.2'
	testCompile 'org.objenesis:objenesis:1.3'
}


